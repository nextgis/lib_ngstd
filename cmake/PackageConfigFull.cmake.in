# - Config file for the @PACKAGE_UPPER_NAME@ package
# It defines the following variables
#  @PACKAGE_UPPER_NAME@_INCLUDE_DIRS - include directories for @PACKAGE_NAME@
#  @PACKAGE_UPPER_NAME@_LIBRARIES    - libraries to link against
#  @PACKAGE_UPPER_NAME@_VERSION      - @PACKAGE_NAME@ version

# Our library dependencies (contains definitions for IMPORTED targets)
set(NGLIBS
    core
    framework
)

foreach(NGLIB ${NGLIBS})
    if(WIN32)
        set(PACKAGE_CONFIG_PATH ${CMAKE_CURRENT_LIST_DIR})
    elseif(APPLE)
        get_filename_component(PACKAGE_CONFIG_PATH "${CMAKE_CURRENT_LIST_DIR}" PATH)
        set(PACKAGE_CONFIG_PATH ${PACKAGE_CONFIG_PATH}/Library/Frameworks/@PACKAGE_NAME@_${NGLIB}.framework/Resources/CMake)
    else()
        set(PACKAGE_CONFIG_PATH ${CMAKE_CURRENT_LIST_DIR})
    endif()

    string(TOUPPER ${NGLIB} UPPER_NGLIB)
    if(NOT TARGET @PACKAGE_NAME@_${NGLIB})
        if(EXISTS "${PACKAGE_CONFIG_PATH}/@PACKAGE_UPPER_NAME@_${UPPER_NGLIB}Config.cmake")
            include("${PACKAGE_CONFIG_PATH}/@PACKAGE_UPPER_NAME@_${UPPER_NGLIB}Config.cmake")
        endif()
    endif()
endforeach()

if(@PACKAGE_UPPER_NAME@_FIND_COMPONENTS)
    foreach(module ${@PACKAGE_UPPER_NAME@_FIND_COMPONENTS})
        string(TOUPPER ${module} MODULE_UPPERNAME)
        set(@PACKAGE_UPPER_NAME@_LIBRARIES ${@PACKAGE_UPPER_NAME@_LIBRARIES} ${@PACKAGE_UPPER_NAME@_${MODULE_UPPERNAME}_LIBRARIES})
        set(@PACKAGE_UPPER_NAME@_INCLUDE_DIRS ${@PACKAGE_UPPER_NAME@_INCLUDE_DIRS} ${@PACKAGE_UPPER_NAME@_${MODULE_UPPERNAME}_INCLUDE_DIRS})
    endforeach()
else()
    foreach(NGLIB ${NGLIBS})
        set(@PACKAGE_UPPER_NAME@_LIBRARIES ${@PACKAGE_UPPER_NAME@_LIBRARIES} ${@PACKAGE_UPPER_NAME@_${UPPER_NGLIB}_LIBRARIES})
        set(@PACKAGE_UPPER_NAME@_INCLUDE_DIRS ${@PACKAGE_UPPER_NAME@_INCLUDE_DIRS} ${@PACKAGE_UPPER_NAME@_${UPPER_NGLIB}_INCLUDE_DIRS})
    endforeach()
endif()

list(REMOVE_DUPLICATES @PACKAGE_UPPER_NAME@_LIBRARIES)

set(@PACKAGE_UPPER_NAME@_VERSION "@VERSION@")
set(@PACKAGE_UPPER_NAME@_FOUND TRUE)
